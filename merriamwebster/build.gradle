// :merriamwebster is a dynamic feature module which includes all code necessary to work with the
// the Merriam-Webster api and display it's data.

apply plugin: 'com.android.dynamic-feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // Merriam Webster key - hidden from VCS
        if (rootProject.file('keys.properties').exists()) {
            def keyProperties = new Properties()
            keyProperties.load(rootProject.file("keys.properties").newDataInputStream())
            def mwApiKey = keyProperties.getProperty("merriamWebster.key", "")
            buildConfigField "String", "MERRIAM_WEBSTER_KEY", mwApiKey

        }
    }

    // View/Data binding
    viewBinding {
        enabled = true
    }

    // Compile options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':app')
    implementation project(':core')

    // TODO: Deprecated in 1.1.0-alpha01 and should be replaced with LiveData
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
}
