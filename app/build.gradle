// :app is the main application module which holds the Waylan's single MainActivity and basic
// user-facing functionality.

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions' //TODO: Remove in favor of viewBinding.
apply plugin: 'io.fabric'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "space.narrate.words.android"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 7
        versionName "0.0.07.debug"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Merriam Webster Key - hidden from VCS
        buildConfigField "String", "MERRIAM_WEBSTER_KEY", "$mwApiKey"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    // Build configurations
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    // View/Data binding
    viewBinding {
        enabled = true
    }

    // Dynamic feature modules
    dynamicFeatures = [":about", ":merriamwebster"]

    // Compile options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        checkReleaseBuilds false
    }

    // Testing
    // Add shared test dir to be used by both unit and instrumented tests
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    // Include Android resources for robolectric unit testing.
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Documentation
    dokka {
        includes = ['app.md']
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }


}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':core')

    // Room
    implementation "androidx.room:room-runtime:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    // Firebase & Firestore
    implementation "com.google.firebase:firebase-core:${versions.firebaseCore}"
    implementation "com.google.firebase:firebase-auth:${versions.firebaseAuth}"
    implementation "com.google.firebase:firebase-firestore:${versions.firestore}"
    implementation "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}"

    // Google Play Billing
    implementation "com.android.billingclient:billing:${versions.googlePlayBilling}"

}

apply plugin: 'com.google.gms.google-services'
