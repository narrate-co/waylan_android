apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../shared_dependencies.gradle'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "space.narrate.words.android"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 7
        versionName "0.0.07.debug"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Merriam Webster Key
        buildConfigField "String", "MERRIAM_WEBSTER_KEY", "$mwApiKey"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    // Add shared test dir to be used by both unit and instrumented tests
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    dokka {
        includes = ['app.md']
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    viewBinding {
        enabled = true
    }

    dynamicFeatures = [":about"]
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':core')

    // AndroidX
    implementation "android.arch.paging:runtime:${versions.paging}"

    // Room
    implementation "androidx.room:room-runtime:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"
    implementation "com.github.humazed:RoomAsset:${versions.roomAsset}"
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'

    // ThreeTen BP - Date & Time and GSON adapter
    implementation "org.threeten:threetenbp:${versions.threetenbp}"
    implementation "org.aaronhe:threetenbp-gson-adapter:${versions.threetenbpGson}"

    // GSON
    implementation "com.google.code.gson:gson:${versions.gson}"

    // Firebase & Firestore
    implementation "com.google.firebase:firebase-core:${versions.firebaseCore}"
    implementation "com.google.firebase:firebase-auth:${versions.firebaseAuth}"
    implementation "com.google.firebase:firebase-firestore:${versions.firestore}"
    implementation "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}"

    // Google Play Billing
    implementation "com.android.billingclient:billing:${versions.googlePlayBilling}"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-simplexml:${versions.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp3}"

    // Unit tests
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.mockito:mockito-inline:${versions.mockito}"
    testImplementation "org.hamcrest:hamcrest-all:${versions.hamcrest}"
    testImplementation "androidx.arch.core:core-testing:${versions.lifecycle}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
    testImplementation "org.robolectric:robolectric:${versions.robolectric}"
    testImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
    testImplementation "androidx.test.espresso:espresso-contrib:${versions.espresso}"
    testImplementation "androidx.test.espresso:espresso-intents:${versions.espresso}"
    testImplementation "com.google.truth:truth:${versions.googleTruth}"
    testImplementation "org.koin:koin-test:${versions.koin}"

    // Android Unit tests
    androidTestImplementation "junit:junit:${versions.junit}"
    androidTestImplementation "org.mockito:mockito-core:${versions.mockito}"
    androidTestImplementation "org.mockito:mockito-inline:${versions.mockito}"

    // AndroidX JVM tests
    testImplementation "androidx.test:core-ktx:${versions.testCoreKtx}"
    testImplementation "androidx.test.ext:junit-ktx:${versions.testExtKotlinRunner}"
    testImplementation "androidx.test:rules:${versions.testRules}"
    // Once https://issuetracker.google.com/127986458 is fixed this can be testImplementation
    implementation "androidx.fragment:fragment-testing:${versions.fragment}"
    implementation "androidx.test:core:${versions.testCore}"
    implementation "androidx.fragment:fragment:${versions.fragment}"

    // Instrumented tests
    androidTestImplementation "androidx.test:core-ktx:${versions.testCoreKtx}"
    androidTestImplementation "androidx.test.ext:junit-ktx:${versions.testExtKotlinRunner}"
    androidTestImplementation "androidx.test:rules:${versions.testRules}"
    androidTestImplementation "androidx.room:room-testing:${versions.room}"
    androidTestImplementation "androidx.arch.core:core-testing:${versions.lifecycle}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${versions.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${versions.espresso}"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:${versions.espresso}"
    androidTestImplementation "org.robolectric:annotations:${versions.robolectric}"
    implementation "androidx.test.espresso:espresso-idling-resource:${versions.espresso}"

    androidTestImplementation "androidx.appcompat:appcompat:${versions.appCompat}"
    androidTestImplementation "com.google.android.material:material:${versions.material}"
}

apply plugin: 'com.google.gms.google-services'
