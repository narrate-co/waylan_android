// :core is a library module which holds dependencies and implementations which are used across
// all modules.

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    // Build configurations
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    // testLib is a set of sources to be shared between both unit and instremented tests as well
    // as across modules with the target '["${project(':core').projectDir}/src/testLib/java"]'.
    sourceSets {
        String testLibDir = 'src/testLib/java'
        test.java.srcDirs += testLibDir
        androidTest.java.srcDirs += testLibDir
    }

    // View/Data binding
    viewBinding {
        enabled = true
    }

    // Compile options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Room
    implementation Libs.roomRuntime
    kapt Libs.roomCompiler

    // Firebase & Firestore
    implementation Libs.firebase
    implementation Libs.firebaseAuth
    implementation Libs.firebaseFirestore
    implementation Libs.crashlytics

    // Google Play Billing
    implementation Libs.playBilling
}
