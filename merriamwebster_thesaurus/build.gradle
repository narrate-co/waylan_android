// :merriamwebster_thesaurus is a dynamic feature module which includes all code necessary to
// work with the the Merriam-Webster Thesaurus api and display it's data.

apply plugin: 'com.android.dynamic-feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk

        // Merriam Webster Thesaurus key - hidden from VCS
        if (rootProject.file('keys.properties').exists()) {
            def keyProperties = new Properties()
            keyProperties.load(rootProject.file("keys.properties").newDataInputStream())
            def mwThesApiKey = keyProperties.getProperty("merriamWebsterThesaurus.key", "")
            buildConfigField "String", "MERRIAM_WEBSTER_THESAURUS_KEY", mwThesApiKey
        }
    }

    // View/Data binding
    buildFeatures {
        viewBinding = true
    }

    // Compile options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':app')
    implementation project(':core')

    implementation Libs.moshiKotlin
    implementation Libs.moshiAdapter

    // Room
    implementation Libs.roomRuntime
    kapt Libs.roomCompiler
}